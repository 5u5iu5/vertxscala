#!groovy

plugins {
  id 'java'
  id 'scala'
  id 'application'
  id 'com.github.johnrengelman.shadow' version '1.2.4'
}

repositories {
  jcenter()
}

def vertxVersion = '3.4.1'
def scalaVersion = '2.12.1'

sourceCompatibility = '1.8'

dependencies {
  compile "org.scala-lang:scala-library:${scalaVersion}"

  compile "io.vertx:vertx-lang-scala_2.12:${vertxVersion}"
  compile "io.vertx:vertx-web-scala_2.12:${vertxVersion}"
}

mainClassName = "io.vertx.core.Launcher"
def mainVerticle = "scala:com.example.Starter"

run {
  args = ["run", mainVerticle]
}

sourceSets {
  main {
    // let Scala compiler do everything â€“ even java stuff
    scala {
      srcDirs = ['src/main/scala', 'src/main/java']
    }

    java {
      srcDirs = []
    }
  }
}

jar {
  manifest {
    attributes 'Main-Class': mainClassName
    attributes 'Main-Verticle': mainVerticle
    attributes 'Implementation-Vendor': project.group
    attributes 'Implementation-Title': project.name
    attributes 'Implementation-Version': project.version
  }
}

shadowJar {
  classifier = 'fat'

  mergeServiceFiles {
    include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '3.4'
}